
    ///////////////////////////////////////////
    // *** Toylang Grammar ***
    //
    // A program is a series of functions.
    // The first function matching is called first
    // Grammar is hopefully ll(1), suitable for a
    // Top-Down parser.
    //
    ///////////////////////////////////////////


    start: (_NEWLINE|function)*

    function: identifier "(" [fn_arglist] ")" ":" _NEWLINE? function_body

    function_body: (expr _NEWLINE)* return_statement "." _NEWLINE?

    return_statement: expr

    ///////////////////////////////////////////
    // Arithmetic Expressions
    ///////////////////////////////////////////

    expr: term [expr_]

    expr_:    "+" term [expr_]  -> arith_add
            | "-" term [expr_]  -> arith_sub
            | "=" term  [expr_] -> lhs_assignment

    term: factor [term_]

    term_:    "*" factor [term_] -> arith_mul
            | "/" factor [term_]

    ///////////////////////////////////////////
    //Boolean Expression
    ///////////////////////////////////////////

    boolean_expr: factor boolean_expr_

    boolean_expr_:    "^" factor [boolean_expr_]
                    | "or" factor [boolean_expr_]
                    | ">" factor [boolean_expr_]
                    | "<" factor [boolean_expr_]
                    | "==" factor [boolean_expr_]
                    | "!=" factor [boolean_expr_] -> bool_neq

    ///////////////////////////////////////////


    factor: "(" expr ")" |  list  | number | factor_ | STRING

    list: "[" [arglist] "]"
           | identifier "~" identifier

    factor_: identifier [arguments]


    arguments: "[" arglist "]"  -> array_index
              | "(" arglist ")" -> function_call_args

    arglist: expr [more_args] ["|" boolean_expr]

    fn_arglist: expr [more_args] ["|" boolean_expr]

    more_args:  "," expr [more_args]


    ///////////////////////////////////////////
    //Terminal Definitions
    ///////////////////////////////////////////

    identifier: NAME

    number: "-" number_
            | number_

    number_: DEC_NUMBER | HEX_NUMBER | FLOAT

    COMMENT: /#[^\n]*/
    _NEWLINE: ( /\r?\n[\t ]*/ | COMMENT )+

    string: STRING

    STRING : /[ubf]?r?("(?!"").*?(?<!\\)(\\\\)*?"|'(?!'').*?(?<!\\)(\\\\)*?')/i
    DEC_NUMBER: /[0-9]\d*l?/i
    HEX_NUMBER: /0x[\da-f]*l?/i

    %import common.FLOAT -> FLOAT
    %import common.INT -> _INT
    %import common.CNAME -> NAME

    %ignore /[\t \f]+/  // WS
    %ignore /\\[\t \f]*\r?\n/   // LINE_CONT
    %ignore COMMENT
    %declare _INDENT _DEDENT

